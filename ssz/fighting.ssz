lib mes = <alpha/mesdialog.ssz>;
lib sdl = <alpha/sdlplugin.ssz>;
lib s = <string.ssz>;
lib m = <math.ssz>;
lib se = <alpha/sdlevent.ssz>;
lib al = <alert.ssz>;
lib sff = "sff.ssz";
lib chr = "char.ssz";
lib cfg = "../save/config.ssz";
lib com = "common.ssz";
lib fnt = "font.ssz";
lib sha = "share.ssz";
lib tbl = <table.ssz>;
lib file = <file.ssz>;
lib cmd = "command.ssz";
lib dscri = "debug-script.ssz";
lib tscri = "trigger-script.ssz";
lib snd = "sound.ssz";

const double lvmul = 2.0**(1.0/12.0);

&WincntMgr
{
  /^/char wincFN = "save/debug/autolevel.txt";
  &.tbl.NameTable!int? winct;
  ^int zeroAry(int sz)
  {
    ^int a.new(sz);
    a:<-[void(i=){i = 0;}];
    ret a;
  }
  public void init()
  {
    if(!.com.autolevel) ret;
    ^char buf;
    buf = .s.u8ToS()<, .file.readAll!ubyte?(`wincFN);
    if(#buf > 0 && buf[0] == '\xfeff') buf = buf[1..-1];
    ^^/char lines = .s.splitLines(buf);
    lines:<-[void(l){
      ^^/char tmp = .s.split(",", l);
      if(#tmp < 2) ret;
      ^int item = .s.svToAry!int?(" ", .s.trim(tmp[1]));
      item = item[0 .. .sff.NumCharPalletes];
      if(#item < .sff.NumCharPalletes){
        item = item + `zeroAry(.sff.NumCharPalletes-#item);
      }
      `winct.set(tmp[0], item);
    }];
  }
  delete()
  {
    if(!.com.autolevel || !.com.matchOver()) ret;
    loop{index i = 0; while; do:
      if(#.chr.chars[i] > 0) branch{
      cond .chr.chars[i][0]~isWin():
        `win(i);
      cond .chr.chars[i][0]~isLose():
        `lose(i);
      }
      i++;
    while i < #.chr.chars:}
    %char buf;
    buf .= '\xfeff';
    `winct.forEach()<, [void(^/char name, ^int cnt){
      buf .= name .= ',';
      loop{index i = 0; while; do:
        buf .= ' ' .= ''cnt[i];
        i++;
      while i < .sff.NumCharPalletes:}
      buf .= \r\n;
    }];
    &.file.File f;
    f.open(`wincFN, "wb");
    f.writeAry!ubyte?(.s.sToU8(buf));
  }
  ^int getItem(^/char name)
  {
    ^int item = `winct.get(name);
    if(#item < .sff.NumCharPalletes){
      item = item + `zeroAry(.sff.NumCharPalletes-#item);
    }
    ret item;
  }
  void setItem(int pn, ^int item)
  {
    int ave = 0;
    loop{
      index i = 0;
      int palcnt = 0;
      while;
    do:
      if(.chr.cgi[pn].palSelectable[i]){
        ave += item[i];
        palcnt++;
      }
      i++;
    while i < #item:
      ave /= palcnt;
    }
    loop{index i = 0; while; do:
      if(!.chr.cgi[pn].palSelectable[i]) item[i] = ave;
      i++;
    while i < #item:}
    `winct.set(.chr.cgi[pn].def, item);
  }
  /int winPoint(index i)
  {
    ret
      .com.tmode[i+1&1] == (int).com.TeamMode::Simul ? (
        .com.tmode[i&1] != (int).com.TeamMode::Simul ? .com.numSimul[i+1&1]
        : .com.numSimul[i+1&1] > .com.numSimul[i&1]
        ? .com.numSimul[i+1&1] / .com.numSimul[i&1] : 1)
      : 1;
  }
  void win(index i)
  {
    ^int item = `getItem(.chr.cgi[i].def);
    item[.chr.chars[i][0]~palno()-1] += `winPoint(i);
    `setItem(i, item);
  }
  void lose(index i)
  {
    ^int item = `getItem(.chr.cgi[i].def);
    item[.chr.chars[i][0]~palno()-1] -= `winPoint(i);
    `setItem(i, item);
  }
  public int getLevel(index i)
  {
    ^int item = `getItem(.chr.cgi[i].def);
    ret item[.chr.cgi[i].palno-1];
  }
}
&WincntMgr wm;

void game()
{
  void put(float y=, ^/char txt)
  {
    bool tmp = .sff.allPalFX~enable;
    .sff.allPalFX~enable = false;
    loop{
      ^/char t = txt;
      index i;
    do:
      i = 0;
      loop{int w = 0; while; do:
        w += .fnt.debugFont~charWidth(t[i]) + .fnt.debugFont~spacingx;
        if(w > .cfg.Width) break;
        i++;
      while i < #t:}
      if(i == 0) i = 1;
      .fnt.debugFont~drawText(
        -(float)(.com.GameWidth-320)/2.0,
        y += (float).fnt.debugFont~sizey/.com.HeightScale,
        1.0/.com.WidthScale,  1.0/.com.HeightScale, 0, 255, 0, .com.scrrect=, 1, t[0..i]);
      t = t[i..-1];
    while #t > 0:}
    .sff.allPalFX~enable = tmp;
  }
  %char line;
  void drawPauseMenu(bool st, bool esc)
  {:<-
    if(.com.pauseMenu == 0) ret;
    .dscri.L.getGlobal("pauseMenu");
    .dscri.L.pushNumber((double)(.com.pauseMenu));
    .dscri.L.pushBoolean(st);
    .dscri.L.pushBoolean(esc);
    if(!.dscri.L.pcall(3, 1)) ret;
  }
  void drawDebug() //DEBUG INFO
  {:<-
    if(!.com.debugdraw || #.fnt.debugFont == 0) ret;
    float y = -(float)(.com.GameHeight-240);
//CHARS STATUS INFO
    if(#.com.debugScript > 0) loop{index i = 0; while; do:
      if(#.chr.chars[i] > 0){
        .dscri.L.getGlobal("status");
        .dscri.L.pushNumber((double)(i+1));
        if(!.dscri.L.pcall(1, 1)) break;
        ^/char status;
        if(.dscri.L.isString(-1)) status = .dscri.L.toString(-1);
        .dscri.L.pop(1);
        if(#status > 0) `put(y=, status);
      }
      i++;
    while i < #.chr.chars:}
//MATCH GLOBAL INFO
    y = .m.max!float?(y, 48.0 - (float)(.com.GameHeight-240));
	`put(y=, "GAME MODE:" + .com.gameMode + " - SERVICE:" + .com.gameService); //Display Game Mode & Service Active
	//`put(y=, "GAME TYPE:" + ''.com.gameType); //Display Game Type
	`put(y=, "PLAYER SIDE:" + .com.playerSide); //Display Player Side
	`put(y=, "TOURNEY STATE:" + .com.tourneyState); //Display Tourney State
	`put(y=, "ABYSS DEPTH:" + ''.com.abyssDepth + " - ABYSS REWARD:" + ''.com.abyssReward + " - NEXT ABYSS DEPTH BOSS:" + ''.com.abyssDepthBoss + " - ABYSS BOSS STATE:" + ''.com.abyssBossFight); //Display Abyss Depth Level, Abyss Reward, Next Abyss Boss Match & Abyss Boss State
	`put(y=, ""); //Enter a space to separate
	`put(y=, "MATCHS TO WIN:" + ''.com.matchsToWin + " - CURRENT MATCH:" + ''.com.match); //Display Matchs to Win (FT) & Current Match Number
	`put(y=, "P1 MATCH WINS:" + ''.com.p1matchWins); //Display Player 1 Match Wins
	`put(y=, "P2 MATCH WINS:" + ''.com.p2matchWins); //Display Player 2 Match Wins
	`put(y=, ""); //Enter a space to separate
	`put(y=, "ROUNDS TO WIN:" + ''.com.roundsToWin + " - CURRENT ROUND:" + ''.com.round); //Display Rounds to Win & Current Round Number
	`put(y=, "P1 ROUND WINS:" + ''.com.p1wins); //Display Player 1 Round Wins
	`put(y=, "P2 ROUND WINS:" + ''.com.p2wins); //Display Player 2 Round Wins
	//`put(y=, "ROUND TIME:" + ''.com.roundTime); //Display Round Time
	`put(y=, "ROUND STATE:" + ''.tscri.cwc~roundState()); //Display Round State
	`put(y=, ""); //Enter a space to separate
	`put(y=, "RECORDING STATE:" + ''.com.recordState + " - PLAYBACK STATE:" + ''.com.playbackState); //Display Dummy Record State & Dummy Playback State
	`put(y=, "GAME SPEED:" + ''.com.Speed); //Display Game Speed
	`put(y=, ""); //Enter a space to separate
//CHARS INFO
    loop{index i = 0; while; do:
      if(#.chr.chars[i] > 0){
	//ACTION INFO
		.dscri.L.getGlobal("actioninfo");
		.dscri.L.pushNumber((double)(i+1));
        if(!.dscri.L.pcall(1, 1)) break;
        ^/char actioninfo;
        if(.dscri.L.isString(-1)) actioninfo = .dscri.L.toString(-1);
        .dscri.L.pop(1);
        if(#actioninfo > 0) `put(y=, actioninfo);
	//STATE INFO	
		.dscri.L.getGlobal("stateinfo");
		.dscri.L.pushNumber((double)(i+1));
        if(!.dscri.L.pcall(1, 1)) break;
        ^/char stateinfo;
        if(.dscri.L.isString(-1)) stateinfo = .dscri.L.toString(-1);
        .dscri.L.pop(1);
        if(#stateinfo > 0) `put(y=, stateinfo);
		`put(y=, ""); //Enter a space to separate
      }
      i++;
    while i < #.chr.chars:}
//CONTENT INFO
	loop{index i = 0; while; do:
      if(#.chr.chars[i] > 0) `put(y=, .chr.cgi[i].def);
      i++;
	while i < #.chr.chars:}
    `put(y=, .chr.stg~def);
	`put(
      y=, (
        #.tscri.cwc > 0
        ? "P"+''(.tscri.cwc~playerno+1)+":" + .tscri.cwc~name : "")
      + "> " + `line);
    loop{index i = 0; while; do:
      if(#.chr.chars[i] > 0) loop{index j = 0; while; do:		
		`put(y=, .com.clipboardText[i][j]);
        j++;
      while j < #.com.clipboardText[i]:}
      y += (float).fnt.debugFont~sizey/.com.HeightScale;
      i++;
    while i < #.chr.chars:}
  }
  void debugInput()
  {
    if(!.com.debugdraw || #.fnt.debugFont == 0) ret;
    char lastChar = .sdl.getLastChar();
    switch(lastChar){
    case '\0':
    case '\r':
      if(#.com.debugScript > 0){
        if(!.dscri.L.runString(`line)) .al.alert!self?(.dscri.L.toString(-1));
      }
      `line.new(0);
    case '\x8':
      {
        ^char tmp = `line[0..#`line-1];
        `line.new(0);
        `line .= tmp;
      }
    default:
      if(lastChar >= '\x20' && lastChar < '\x7f') `line .= lastChar;
    }
  }
  float x = 0.0, y = 0.0, l = 0.0, r = 0.0, bl = 0.0, br = 0.0;
  float scl = 1.0, sclmul = 1.0;
  float newx = x, newy = y;
  int oldp1wins, oldp2wins, olddraws;
  bool pmSt = false, pmEsc = false;
  ^int lif.new(#.chr.chars), pow.new(#.chr.chars);
  ^^/int var.new(#.chr.chars), tvar.new(#.chr.chars);
  ^^/float fvar.new(#.chr.chars), tfvar.new(#.chr.chars);
  void copyVar(int pno)
  {
    `lif[pno] = .chr.chars[pno][0]~sysivar[.chr.iLIFE];
    `pow[pno] = .chr.chars[pno][0]~sysivar[.chr.iPOWER];
    `var[pno] = .s.clone!int?(.chr.chars[pno][0]~ivar);
    `fvar[pno] = .s.clone!float?(.chr.chars[pno][0]~fvar);
  }
  void reset()
  {
    .com.p1wins = `oldp1wins;
    .com.p2wins = `oldp2wins;
    .com.draws = `olddraws;
	.com.forceOver = false;
    loop{index i = 0; do:
      if(#.chr.chars[i] > 0){
        .chr.chars[i][0]~sysivar[.chr.iLIFE] = `lif[i];
        .chr.chars[i][0]~sysivar[.chr.iPOWER] = `pow[i];
        .s.copy!int?(.chr.chars[i][0]~ivar, `var[i]);
        .s.copy!float?(.chr.chars[i][0]~fvar, `fvar[i]);
      }
      i++;
    while i < #.chr.chars:}
    .com.resetFrameTime();
    .chr.nextRound();
    `x = `newx = 0.0;
    `y = `newy = 0.0;
    `l = `r = 0.0;
    `scl = `sclmul = 1.0;
	`pmSt = `pmEsc = false;
    .com.cam.update!.chr.stg<>.self?(`scl, `x, `y, .chr.stg<>=);
  }
  loop{
    int stagetime;
    ^&.sha.Share share;
    &.com.SuperDangerousRef oldshare;
    oldshare.copyRef!&.sha.Share?(share=);
    ^char ss;
    .mes.GetSharedString(:ss=:);
    ^^/char sss = .s.split("<>", ss);
    &.com.SuperDangerousRef sdr;
    sdr.pos = 0;
    sdr.l = typesize(&.sha.Share);
    sdr.p = .s.sToN!index?(sss[1]);
    sdr.copyToRef!&.sha.Share?(share=);
    share~push();
    ^/char err;
    if(#.com.debugScript > 0 && #.cmd.net == 0){
      err = .dscri.loadFile(.com.debugScript);
      if(#err > 0) .al.alert!.self?(err);
    }
    stagetime = share~stt;
    .cmd.synchronize();
    .wm.init();
    ^int level.new(#.chr.chars);
    loop{index i = 0; while; do:
      level[i] = 0;
      if(#.chr.chars[i] > 0){
        .chr.chars[i]<>~rootInit();
        level[i] = .wm.getLevel(i);
        if(.com.powerShare[i&1]){
          int pmax =
            .m.max!int?(
              .chr.chars[i&1]<>~sysivar[.chr.iPOWERMAX],
              .chr.chars[i]<>~sysivar[.chr.iPOWERMAX]);
          loop{index j = i&1; while; do:
            .chr.chars[j]<>~sysivar[.chr.iPOWERMAX] = pmax;
            j += 2;
          while j <= i:}
        }
      }
      i++;
    while i < #.chr.chars:}
    int minlv = level[0], maxlv = level[0];
    loop{index i = 1; while; do:
      if(#.chr.chars[i] > 0){
        minlv = .m.min!int?(minlv, level[i]);
        maxlv = .m.max!int?(maxlv, level[i]);
      }
      i++;
    while i < #.chr.chars:}
    branch{
    cond minlv > 0:
      loop{index i = 0; while; do:
        level[i] -= minlv;
        i++;
      while i < #.chr.chars:}
    cond maxlv < 0:
      loop{index i = 0; while; do:
        level[i] -= maxlv;
        i++;
      while i < #.chr.chars:}
    }
    loop{index i = 0; do:
      if(#.chr.chars[i] == 0) continue;
      .chr.players.add(.chr.chars[i][0]);
      {
        float lm = (float).chr.chars[i][0]~sysivar[.chr.iLIFEMAX];
        lm *= .com.life;		
        switch(.com.tmode[i&1]){
        case (int).com.TeamMode::Single:
          switch(.com.tmode[i+1&1]){
          case (int).com.TeamMode::Simul:
            lm *= .com.team1VS2Life;
          case (int).com.TeamMode::Turns:
            if(.com.numturns[i+1&1] < .com.roundsToWin && .com.sharedLife){
              lm =
                lm*(float).com.numturns[i+1&1]
                / (float).com.roundsToWin;
            }
          }
        case (int).com.TeamMode::Simul:
          if(.com.sharedLife){
            switch(.com.tmode[i+1&1]){
            case (int).com.TeamMode::Simul:
              if(.com.numSimul[i+1&1] < .com.numSimul[i&1]){
                lm = lm*(float).com.numSimul[i+1&1] / (float).com.numSimul[i&1];
              }
            case (int).com.TeamMode::Turns:
              if(
                .com.numturns[i+1&1]
                < .com.numSimul[i&1] * .com.roundsToWin)
              {
                lm =
                  lm*(float).com.numturns[i+1&1]
                  / (float)(.com.numSimul[i&1] * .com.roundsToWin);
              }
            default:
              lm /= (float).com.numSimul[i&1];
            }
          }
        case (int).com.TeamMode::Turns:
          switch(.com.tmode[i+1&1]){
          case (int).com.TeamMode::Single:
            if(.com.roundsToWin < .com.numturns[i&1] && .com.sharedLife){
              lm =
                lm * (float).com.roundsToWin
                / (float).com.numturns[i&1];
            }
          case (int).com.TeamMode::Simul:
            if(
              .com.numSimul[i+1&1] * .com.roundsToWin
              < .com.numturns[i&1])
            {
              lm =
                lm * .com.team1VS2Life
                * (float)(.com.numSimul[i+1&1] * .com.roundsToWin)
                / (float).com.numturns[i&1];
            }
          case (int).com.TeamMode::Turns:
            if(.com.numturns[i+1&1] < .com.numturns[i&1] && .com.sharedLife){
              lm = lm*(float).com.numturns[i+1&1] / (float).com.numturns[i&1];
            }
          }
        }
        double hoge = .lvmul**((double)-level[i]);
        .chr.chars[i][0]~sysivar[.chr.iLIFEMAX] =
          .m.max!int?(1, (int).m.floor(hoge * lm));
        if(.com.rexisted[i&1] > 0){
          .chr.chars[i][0]~sysivar[.chr.iLIFE] =
            .m.min!int?(.chr.chars[i][0]~sysivar[.chr.iLIFEMAX])<, 
            (int).m.ceil(hoge * (float).chr.chars[i][0]~sysivar[.chr.iLIFE]);
        }
      }
      if(
        .com.round == 1 || (
          .com.tmode[i&1] == (int).com.TeamMode::Turns
          && .com.rexisted[i&1] == 0))
      {
        .chr.chars[i][0]~sysivar[.chr.iLIFE] =
          .chr.chars[i][0]~sysivar[.chr.iLIFEMAX];
		  //.com.clsndraw = false;
        if(.com.round == 1) .chr.chars[i][0]~sysivar[.chr.iPOWER] = 0; //.com.power; //setPowerMul()
		//if(.com.round == 1) .chr.chars[i][0]~sysivar[.chr.iATTACK] = .com.attack; //setAttackMul()
		//if(.com.round == 1) .chr.chars[i][0]~sysivar[.chr.iDEFENCE] = .com.defence; //setDefenceMul()
	//SET HANDICAP VIA DEBUG.LUA START
		//if(.com.round == 1) .tscri.cwc~setPower(2000);
        //if(.com.round == 1) {
			.dscri.L.getGlobal("handicapSet"); //Read lua function from debug.lua
			.dscri.L.pushNumber((double)(i+1)); //players arguments: f_name(p)
			if(!.dscri.L.pcall(1, 1)) ret; //arguments
		//}
	//SET HANDICAP VIA DEBUG.LUA END
	
	//SET ABYSS CHAR STATS VIA DEBUG.LUA START
		//if(.com.round == 1) {
			.dscri.L.getGlobal("statsSet");
			.dscri.L.pushNumber((double)(i+1));
			if(!.dscri.L.pcall(1, 1)) ret;
		//}
	//SET ABYSS CHAR STATS VIA DEBUG.LUA END
      }
	  //if(.s.equ(.com.gameMode, "practice")){
        //branch{
        //cond .com.round != -1:
          //.com.round = -1;
          //.com.rexisted[0] = .com.rexisted[1] = 0;
        //else:
        //.chr.chars[i][0]~sysivar[.chr.iLIFE] =
          //.chr.chars[i][0]~sysivar[.chr.iLIFEMAX];
        //}
      //}
      copyVar(i);
    continue:
      i++;
    while i < #.chr.chars:}
    .chr.stg~reset();//reset here due to bgctrl (manipulating types that can be referenced cyclically)
    loop{int i = 0; while; do:
      .chr.stg~action();
      i++;
    while i < stagetime:}
	.com.sysControls = 0; //Fight
    .com.gameState = 1;
    //if(.com.round == 1) .snd.bgm~play(.chr.stg~bgmusic);
    .com.cam.init();
    .com.screenleft = (float).chr.stg~screenleft * .chr.stg~localscl;
    .com.screenright = (float).chr.stg~screenright * .chr.stg~localscl;
    float tmp;
    oldp1wins = .com.p1wins;
    oldp2wins = .com.p2wins;
    olddraws = .com.draws;
    reset();
  do:
    .com.step = false;
    .dscri.roundResetFlg = false;
    .dscri.reloadFlg = false;
    loop{index i = 0; while; do:
      if(.se.eventKeys[i].down){
        ^^/char scri = .com.hotkeys.get(.com.eventKeyHash(.se.eventKeys[i]));
        if(#scri > 0 && !.dscri.L.runString(scri<>)){
          .al.alert!self?(.dscri.L.toString(-1));
        }
      }
      i++;
    while i < #.se.eventKeys:}
	branch{
    cond .cmd.startButton(1) && .com.pauseMenu != 2: .dscri.L.runString("togglePauseMenu(1)");
    cond .cmd.startButton(2) && .com.pauseMenu != 1 && .com.com[1] == 0: .dscri.L.runString("togglePauseMenu(2)");
    }
    if(.dscri.roundResetFlg){
	  reset();
	}
    if(.dscri.reloadFlg){
      .mes.SetSharedString(:"reload":);
      ret;
    }
    if(.chr.roundOver() && !.s.equ(.com.gameMode,"practice")) branch{
      .com.round++;
      loop{index i = 0; while; do:
        .com.rexisted[i]++;
        i++;
      while i < #.com.rexisted:}
    cond
      !.com.matchOver()
      && (
        .com.tmode[0] != (int).com.TeamMode::Turns
        || .chr.chars[0][0]~isWin())
      && (
        .com.tmode[1] != (int).com.TeamMode::Turns
        || .chr.chars[1][0]~isWin())
      :
      loop{index i = 0; while; do:
        if(#.chr.chars[i] > 0){
          branch{
          cond
            .com.tmode[i&1] != (int).com.TeamMode::Turns
            || !.chr.chars[i]<>~isWin()
            :
            .chr.chars[i]<>~sysivar[.chr.iLIFE] =
              .chr.chars[i]<>~sysivar[.chr.iLIFEMAX];
          else:
            .m.limMin!int?(.chr.chars[i]<>~sysivar[.chr.iLIFE]=, 1);
          }
          copyVar(i);
        }
        i++;
      while i < #.chr.chars:}
      oldp1wins = .com.p1wins;
      oldp2wins = .com.p2wins;
      olddraws = .com.draws;
      reset();
    else:
      loop{index i = 0; while; do:
        branch{
        cond
          .chr.chars[i][0]~isWin() || (
            !.chr.chars[i][0]~isLose()
            && .com.tmode[i] != (int).com.TeamMode::Turns)
          :
          loop{index j = i; while; do:
            if(#.chr.chars[j] > 0){
              .chr.chars[j][0]~sysivar[.chr.iLIFE] =
                .m.max!int?(1, .chr.chars[j][0]~isWin() ? (int).m.ceil(
                  .lvmul**(double)level[j] * (float)(
                    .chr.chars[j][0]~sysivar[.chr.iLIFE]))
                : .chr.chars[j][0]~sysivar[.chr.iLIFEMAX2]);
            }
            j += 2;
          while j < #.chr.chars:}
        else:
          .chr.chars[i][0]~sysivar[.chr.iLIFE] = 0;
        }
        i++;
      while i < #.com.tmode:}
      break, break;
    }
    if(.chr.roundOver() && .s.equ(.com.gameMode, "practice")) reset();
	if(.com.turbo < 1.0) sclmul **= .com.turbo;
    scl = .com.cam.scaleBound(scl * sclmul);
    tmp = ((float).com.GameWidth/2.0) / scl;
    if(#((l + r) - (newx - x)*2.0) >= tmp / 2.0){
      tmp =
        .m.max!float?(0.0)<, .m.min!float?(tmp)<,
        .m.max!float?((newx-x)-l, r-(newx-x));
    }
    x =
      .com.cam.xBound(scl)<,
      .m.min!float?(x + l + tmp)<, .m.max!float?(x + r - tmp)<, newx;
    if(!.com.cam.zoom) x = .m.ceil(x*4.0 - 0.5)/4.0;
    y = .com.cam.yBound(scl, newy);
    if(
      .com.tickFrame() && !.chr.timestop && (.chr.super <= 0 || .chr.superpausebg == 0)
      && (.chr.pause <= 0 || .chr.pausebg == 0)) 
    {
      .chr.stg~action();
      stagetime++;
    }
    newx = x;
    newy = y;
    sclmul = .chr.action(newx=, newy=, l=, r=, scl);
    debugInput();
    if(!.com.addFrameTime(.com.turbo)){
      if(!.se.eventUpdate()) break, while;
      break, do;
    }
	if(.cmd.startButton(.com.pauseMenu)) pmSt = true;
    if(.se.esc) pmEsc = true;
    if(.se.fskip) while;
    {
      float dx = x, dy = y, dscl = scl;
      if(
        !.m.isnan(.com.drawscale)
        && !.m.isnan(.com.zoomposx)&& !.m.isnan(.com.zoomposy))
      {
        dscl =
          .m.max!float?(
            .com.cam.minScale, .com.drawscale / .com.cam.baseScale());
        dx = .com.cam.xBound(dscl, x + .com.zoomposx * (dscl - scl) / dscl);
        dy = y + .com.zoomposy;
      }
      .chr.draw(dx, dy, dscl);
    }
//ABYSS BOSS CHALLENGER SCREEN
	if (.com.abyssDepth == .com.abyssDepthBoss) {
		.dscri.L.getGlobal("abyssBossChallenger");
		if(!.dscri.L.pcall(0, 1)) ret;
	}
//
	drawPauseMenu(pmSt, pmEsc);
	pmSt = pmEsc = false;
    drawDebug();
  /?/*.cfg.OpenGL:
    .sdl.GlSwapBuffers(::);
  /*true:
    .sdl.flip();
  /*?*/
  while !(.se.esc && (.com.pause || #err > 0 || #.cmd.net > 0)) && !.com.exitMatch && .cmd.update():
    if(.se.esc || .com.exitMatch) .mes.SetSharedString(:"esc":);
  break:
	.com.sysControls = 0; //Fight, set back to 10;
	.com.gameState = 0;
    .chr.stg~bgctl.clear();//Open bgctrl with fighting
    if(#.cmd.net > 0) .cmd.net~stop();
    share~copy();
    share~stt = stagetime;
    oldshare.copyToRef!&.sha.Share?(share=);
  }
  if(!.se.end && .s.equ(.com.gameMode, "practice") && #.chr.chars > 1 && .s.equ(.chr.chars[1]<>~name, "Training") && #.com.debugScript > 0){
    .dscri.L.getGlobal("trnMacroCheck");
    if(!.dscri.L.pcall(0, 1)) ret;
  }
}

public void main()
{
  .game();
  if(.se.end) .mes.SetSharedString(:"end":);
}
