
[Statedef 0]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(
      (`animno != 0 && `animno != 5)
      || (`animno == 5 && `trAnimTime() == 0))
    {
      `trChangeAnim(0);
    }
    if(`stVal.time == 0) `setYV(0.0);
    if(
      #`sysfvar[.fVX]
      < (`mver0() == 1 ? `sysfvar[.fSTAND_FRICTION_THRESHOLD] : 2.0)
      || `stVal.time == 4)
    {
      `setXV(0.0);
    }
    if(!`isAlive()){
      `trChangeState(5050, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::S;
  `sprpriority = 0;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 10]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0) `mulXV(0.75);
    if(#`sysfvar[.fVX] < `sysfvar[.fCROUCH_FRICTION_THRESHOLD]){
      `setXV(0.0);
    }
    if(`trAnimTime() == 0){
      `trChangeState(11, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::C;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::C;
  `trChangeAnim(10);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 11]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`animno == 6 && `trAnimTime() == 0) `trChangeAnim(11);
    if(#`sysfvar[.fVX] < `sysfvar[.fCROUCH_FRICTION_THRESHOLD]){
      `setXV(0.0);
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::C;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::C;
  `trChangeAnim(11);
  `sprpriority = 0;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 12]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`trAnimTime() == 0){
      `trChangeState(0, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::S;
  `trChangeAnim(12);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 20]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`commandByName("holdfwd")){
      `setXV(`sysfvar[.fWALK_FWD]);
    }
    if(`commandByName("holdback")){
      `setXV(`sysfvar[.fWALK_BACK]);
    }
    if(
      `sysfvar[.fVX] > 0.0 && (
        (`animno != 20 && `animno != 5)
        || (`animno == 5 && `trAnimTime() == 0)))
    {
      `trChangeAnim(20);
    }
    if(
      `sysfvar[.fVX] < 0.0 && (
        (`animno != 21 && `animno != 5)
        || (`animno == 5 && `trAnimTime() == 0)))
    {
      `trChangeAnim(21);
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::S;
  `sprpriority = 0;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 40]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0) `sysivar[1] = 0;
    if(`commandByName("holdfwd")) `sysivar[1] = 1;
    if(`commandByName("holdback")) `sysivar[1] = -1;
    if(`trAnimTime() == 0){
      branch{
      cond `stVal.prevno == 100 && `sysivar[1] == 1:
        `setXV(`sysfvar[.fRUNJUMP_FWDX]);
      else:
        `setXV(
          `sysivar[1] == 0 ? `sysfvar[.fJUMP_NEUX] : (
            `sysivar[1] == 1 ? `sysfvar[.fJUMP_FWD] : `sysfvar[.fJUMP_BACK]));
      }
      `setYV(`sysfvar[.fJUMP_NEUY]);
      `trChangeState(50, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::S;
  `trChangeAnim(40);
  `setCtrl(false);
  `sprpriority = 1;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 45]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    branch{
    cond #`getAction(44) > 0:
      `trChangeAnim(44);
    else:
      `trChangeAnim(41);
    }
    if(`stVal.time == 0) `sysivar[1] = 0;
    if(`commandByName("holdfwd")) `sysivar[1] = 1;
    if(`commandByName("holdback")) `sysivar[1] = -1;
    if(`stVal.time == 2){
      `setXV(
        `sysivar[1] == 0 ? `sysfvar[.fAIRJUMP_NEUX] : (
          `sysivar[1] == 1
          ? `sysfvar[.fAIRJUMP_FWD] : `sysfvar[.fAIRJUMP_BACK]));
      `setYV(`sysfvar[.fAIRJUMP_NEUY]);
      `trChangeState(50, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `setCtrl(false);
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 50]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      `sysivar[1] = 0;
      `trChangeAnim(
        `sysfvar[.fVX] == 0.0 ? 41 : (`sysfvar[.fVX] > 0.0 ? 42 : 43));
    }
    if(
      --`stVal.ps[0].p <= 0
      && `sysfvar[.fVY] > -2.0 && #`getAction(`animno + 3) > 0
      && (`mver0() != 1 || .m.inRange!int?(41, 43, `animno)))
    {
      `stVal.ps[0].p = .consts.int_t::MAX;
      `trChangeAnim(`animno + 3);
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::A;
  `stVal.ps.new(1);
  `stVal.ps[0].p = 0;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 51]
SSZ_CODE:
void c.Char::s##_@@P()
{
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::A;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 52]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    switch(`stVal.time){
    case 0:
      `setYV(0.0);
      `setY(0.0);
    case 3:
      `setCtrl(true);
    }
    if(`mver0() == 1 && #`sysfvar[.fVX] < `sysfvar[.fSTAND_FRICTION_THRESHOLD]){
      `setXV(0.0);
    }
    if(`trAnimTime() == 0){
      `trChangeState(0, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::S;
  `setCtrl(false);
  `trChangeAnim(47);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 100]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    `setXV(`sysfvar[.fRUN_FWDX]);
    `sysivar[.iSPECIALFLAG] |= .lsNOWALK | .lsNOAUTOTURN;
    if(!`commandByName("holdfwd")){
      `trChangeState(0, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::S;
  `trChangeAnim(100);
  `sprpriority = 1;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 105]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    switch(`stVal.time){
    case 0:
      `setXV(`sysfvar[.fRUN_BACKX]);
      `setYV(`sysfvar[.fRUN_BACKY]);
    case 2:
      `setCtrl(true);
    }
    if(`sysfvar[.fVY] > 0.0 && `sysfvar[.fY] >= 0.0){
      `trChangeState(106, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::A;
  `setCtrl(false);
  `trChangeAnim(105);
  `sprpriority = 1;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 106]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`mver0() == 1 && #`sysfvar[.fVX] < `sysfvar[.fSTAND_FRICTION_THRESHOLD]){
      `setXV(0.0);
    }
    switch(`stVal.time){
    case 0:
      `setYV(0.0);
      `setY(0.0);
    case 2:
      branch{
      cond `mver0() == 1:
        `makeDust(
          -(float)`sysivar[.iDRAW_OFFSETX], -(float)`sysivar[.iDRAW_OFFSETY]);
      else:
        `makeDust(
          -5.0 - (float)`sysivar[.iDRAW_OFFSETX],
          -2.0 - (float)`sysivar[.iDRAW_OFFSETY]);
      }
    case 7:
      `trChangeState(0, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::S;
  `setCtrl(false);
  `trChangeAnim(47);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 110]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      `setCtrl(false);
      `trChangeAnim(110);
      `stVal.hb.hitf1 = 9351;
      `stVal.hb.hitt1 = 30;
      `setXV(0.0);
    }
    if(`trAnimTime() == 0){
      `trChangeState(0, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 115]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      `setCtrl(false);
      `trChangeAnim(110);
      `stVal.hb.hitf1 = 9351;
      `stVal.hb.hitt1 = 30;
      `setXV(0.0);
    }
    if(`trAnimTime() == 0){
      `trChangeState(0, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 120]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      `trChangeAnim(
        120 + (int)(`stVal.typ == .StTy::C) + (int)(`stVal.typ == .StTy::A)*2);
      switch(`stVal.typ){
      case .StTy::S:
        `stVal.phy = .StTy::S;
      case .StTy::C:
        `stVal.phy = .StTy::C;
      case .StTy::A:
        `stVal.phy = .StTy::A;
      }
    }
    switch(`stVal.typ){
    case .StTy::S:
      if(`commandByName("holddown")){
        `stVal.typ = .StTy::C;
        `stVal.phy = .StTy::C;
      }
    case .StTy::C:
      if(!`commandByName("holddown")){
        `stVal.typ = .StTy::S;
        `stVal.phy = .StTy::S;
      }
    }
    if(`trAnimTime() == 0){
      `trChangeState(
        130 + (int)(`stVal.typ == .StTy::C) + (int)(`stVal.typ == .StTy::A)*2,
        -1, -1);
      ret;
    }
    if(!`commandByName("holdback") || !`inguarddist){
      `trChangeState(140, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.mov = .MovTy::I;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 130]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`animno != 130) `trChangeAnim(130);
    if(`commandByName("holddown")){
      `trChangeState(131, -1, -1);
      ret;
    }
    if(!`commandByName("holdback") || !`inguarddist){
      `trChangeState(140, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::S;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 131]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`animno != 131) `trChangeAnim(131);
    if(!`commandByName("holddown")){
      `trChangeState(130, -1, -1);
      ret;
    }
    if(!`commandByName("holdback") || !`inguarddist){
      `trChangeState(140, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::C;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::C;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 132]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`animno != 132) `trChangeAnim(132);
    `addYV(`sysfvar[.fYACCEL]);
    `sysivar[0] = (int)(`sysfvar[.fY] >= 0.0 && `sysfvar[.fVY] > 0.0);
    if(`sysivar[0] != 0){
      `setYV(0.0);
      `setY(0.0);
      if(`commandByName("holdback") && `inguarddist){
        `trChangeState(130, -1, -1);
        ret;
      }
      `trChangeState(52, -1, -1);
      ret;
    }
    if(!`commandByName("holdback") || !`inguarddist){
      `trChangeState(140, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 140]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      `trChangeAnim(
        140 + (int)(`stVal.typ == .StTy::C) + (int)(`stVal.typ == .StTy::A)*2);
      switch(`stVal.typ){
      case .StTy::S:
        `stVal.phy = .StTy::S;
      case .StTy::C:
        `stVal.phy = .StTy::C;
      case .StTy::A:
        `stVal.phy = .StTy::A;
      }
    }
    switch(`stVal.typ){
    case .StTy::S:
      if(`commandByName("holddown")){
        `stVal.typ = .StTy::C;
        `stVal.phy = .StTy::C;
      }
    case .StTy::C:
      if(!`commandByName("holddown")){
        `stVal.typ = .StTy::S;
        `stVal.phy = .StTy::S;
      }
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.mov = .MovTy::I;
  `setCtrl(true);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 150]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    `trChangeAnim(150);
    if(`trHitShakeOver()){
      `trChangeState(151 + (int)`commandByName("holddown")*2, -1, -1);
      ret;
    }
    switch(`stVal.typ){
    case .StTy::S:
      if(`commandByName("holddown")){
        `stVal.typ = .StTy::C;
        `stVal.phy = .StTy::C;
      }
    case .StTy::C:
      if(!`commandByName("holddown")){
        `stVal.typ = .StTy::S;
        `stVal.phy = .StTy::S;
      }
    }
    if(`stVal.time == 0){
      //ForceFeedback
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 151]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0) `trHitVelSetX();
    if(`stVal.time == `stVal.hb.slidetime || `trHitOver()){
      `setXV(0.0);
    }
    if(`stVal.time == `stVal.hb.ctrltime) `setCtrl(true);
    switch(`stVal.typ){
    case .StTy::S:
      if(`commandByName("holddown")){
        `stVal.typ = .StTy::C;
        `stVal.phy = .StTy::C;
      }
    case .StTy::C:
      if(!`commandByName("holddown")){
        `stVal.typ = .StTy::S;
        `stVal.phy = .StTy::S;
      }
    }
    if(`trHitOver()){
      `trChangeState(130, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::S;
  `trChangeAnim(150);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 152]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    `trChangeAnim(151);
    if(`trHitShakeOver()){
      `trChangeState(151 + (int)`commandByName("holddown")*2, -1, -1);
      ret;
    }
    switch(`stVal.typ){
    case .StTy::S:
      if(`commandByName("holddown")){
        `stVal.typ = .StTy::C;
        `stVal.phy = .StTy::C;
      }
    case .StTy::C:
      if(!`commandByName("holddown")){
        `stVal.typ = .StTy::S;
        `stVal.phy = .StTy::S;
      }
    }
    if(`stVal.time == 0){
      //ForceFeedback
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::C;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 153]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0) `trHitVelSetX();
    if(`stVal.time == `stVal.hb.slidetime || `trHitOver()){
      `setXV(0.0);
    }
    if(`stVal.time == `stVal.hb.ctrltime) `setCtrl(true);
    switch(`stVal.typ){
    case .StTy::S:
      if(`commandByName("holddown")){
        `stVal.typ = .StTy::C;
        `stVal.phy = .StTy::C;
      }
    case .StTy::C:
      if(!`commandByName("holddown")){
        `stVal.typ = .StTy::S;
        `stVal.phy = .StTy::S;
      }
    }
    if(`trHitOver()){
      `trChangeState(131, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::C;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::C;
  `trChangeAnim(151);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 154]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    `trChangeAnim(152);
    if(`trHitShakeOver()){
      `trChangeState(155, -1, -1);
      ret;
    }
    if(`stVal.time == 0){
      //ForceFeedback
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 155]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      `trHitVelSetX();
      `trHitVelSetY();
    }
    `addYV(`sysfvar[.fYACCEL]);
    if(`stVal.time == `stVal.hb.ctrltime) `setCtrl(true);
    `sysivar[0] = (int)(`sysfvar[.fY] >= 0.0 && `sysfvar[.fVY] > 0.0);
    if(`sysivar[0] != 0){
      `setYV(0.0);
      `setY(0.0);
      if(`commandByName("holdback") && `inguarddist){
        `trChangeState(130, -1, -1);
        ret;
      }
      `trChangeState(52, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `trChangeAnim(152);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 170]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    `stVal.hb.hitf1 = -8;
    `stVal.hb.hitt1 = 1;
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `setCtrl(false);
  `trChangeAnim(170);
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 175]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      if(#`getAction(175) == 0){
        `trChangeState(170, -1, -1);
        ret;
      }
      `trChangeAnim(175);
    }
    `stVal.hb.hitf1 = -8;
    `stVal.hb.hitt1 = 1;
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `setCtrl(false);
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 190]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(#`getAction(190) > 0) `trChangeAnim(190);
    if(`stVal.time == 0){
      `trChangeState(191, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `setCtrl(false);
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 191]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      `trChangeState(0, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `setCtrl(false);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5000]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    branch{
    cond `stVal.time == 0:
      switch(`gethitAnimtype()){
      case 4, 5:
        if(#`getAction(5047 + `gethitAnimtype()) > 0){
          `trChangeAnim(5047 + `gethitAnimtype());
          break, break;
        }
      case 3:<-
        `trChangeAnim(5030);
      default:
        `trChangeAnim(
          (`stVal.hb.groundtype == 1 ? 5000 : 5010) + `gethitAnimtype());
      }
    cond `stVal.time > 0:
      `trChangeAnim(`animno);
    }
    if(
      (`stVal.time == 0 && (`stVal.hb.yvel != 0.0 || `isFall()))
      || `sysfvar[.fY] != 0.0)
    {
      `stVal.typ=.StTy::A;
    }
    if(`trHitShakeOver()){
      if(`stVal.hb.yvel == 0.0 && !`isFall()){
        `trChangeState(5001, -1, -1);
        ret;
      }
      `trChangeState(5030, -1, -1);
      ret;
    }
    switch(`animno){
    case 5000, 5010:
      if(--`stVal.ps[0].p > 0) break;
      `stVal.ps[0].p = .consts.int_t::MAX;
      //ForceFeedback
    case 5001, 5011:
      if(--`stVal.ps[1].p > 0) break;
      `stVal.ps[1].p = .consts.int_t::MAX;
      //ForceFeedback
    case 5012, 5002, 5030:
      if(--`stVal.ps[2].p > 0) break;
      `stVal.ps[2].p = .consts.int_t::MAX;
      //ForceFeedback
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `stVal.ps.new(3);
  loop{index i = 0; do:
    `stVal.ps[i].p = 0;
  while ++i < 3:
  }
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5001]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0) `trHitVelSetX();
    if(`trAnimTime() == 0){
      `trChangeAnim(
        5005 + `gethitAnimtype() + (int)(`stVal.hb.groundtype == 2)*10);
    }
    if(`stVal.time >= `stVal.hb.slidetime) `mulXV(0.6);
    if(`trHitOver()){
      `setXV(0.0);
      `defencemul = (float)`sysivar[.iDEFENCE] / 100.0;
      `trChangeState(0, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::S;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5010]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    branch{
    cond `stVal.time == 0:
      switch(`gethitAnimtype()){
      case 4, 5:
        if(#`getAction(5047 + `gethitAnimtype()) > 0){
          `trChangeAnim(5047 + `gethitAnimtype());
          break, break;
        }
      case 3:<-
        `trChangeAnim(5030);
      default:
        `trChangeAnim(5020 + `gethitAnimtype());
      }
    cond `stVal.time > 0:
      `trChangeAnim(`animno);
    }
    if(
      (`stVal.time == 0 && (`stVal.hb.yvel != 0.0 || `isFall()))
      || `sysfvar[.fY] != 0.0)
    {
      `stVal.typ=.StTy::A;
    }
    if(`trHitShakeOver()){
      if(`stVal.hb.yvel == 0.0 && !`isFall()){
        `trChangeState(5011, -1, -1);
        ret;
      }
      `trChangeState(5030, -1, -1);
      ret;
    }
    switch(`animno){
    case 5020:
      if(--`stVal.ps[0].p > 0) break;
      `stVal.ps[0].p = .consts.int_t::MAX;
      //ForceFeedback
    case 5021:
      if(--`stVal.ps[1].p > 0) break;
      `stVal.ps[1].p = .consts.int_t::MAX;
      //ForceFeedback
    case 5022:
      if(--`stVal.ps[2].p > 0) break;
      `stVal.ps[2].p = .consts.int_t::MAX;
      //ForceFeedback
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::C;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `stVal.ps.new(3);
  loop{index i = 0; do:
    `stVal.ps[i].p = 0;
  while ++i < 3:
  }
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5011]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0) `trHitVelSetX();
    if(`trAnimTime() == 0) `trChangeAnim(5025 + `gethitAnimtype());
    if(`stVal.time >= `stVal.hb.slidetime) `mulXV(0.6);
    if(`trHitOver()){
      `setXV(0.0);
      `defencemul = (float)`sysivar[.iDEFENCE] / 100.0;
      `trChangeState(11, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::C;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::C;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5020]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    branch{
    cond `stVal.time == 0:
      switch(`gethitAnimtype()){
      case 4, 5:
        if(#`getAction(5047 + `gethitAnimtype()) > 0){
          `trChangeAnim(5047 + `gethitAnimtype());
          break, break;
        }
      case 3:<-
        `trChangeAnim(5030);
      default:
        `trChangeAnim(
          (`stVal.hb.airtype == 1 ? 5000 : 5010) + `gethitAnimtype());
      }
    cond `stVal.time > 0:
      `trChangeAnim(`animno);
    }
    if(`trHitShakeOver()){
      `trChangeState(5030, -1, -1);
      ret;
    }
    switch(`animno){
    case 5000, 5010:
      if(--`stVal.ps[0].p > 0) break;
      `stVal.ps[0].p = .consts.int_t::MAX;
      //ForceFeedback
    case 5001, 5011:
      if(--`stVal.ps[1].p > 0) break;
      `stVal.ps[1].p = .consts.int_t::MAX;
      //ForceFeedback
    case
      5012, 5002, 5030, 5051, 5052, 5053, 5054, 5055, 5056, 5057, 5058, 5059
      :
      if(--`stVal.ps[2].p > 0) break;
      `stVal.ps[2].p = .consts.int_t::MAX;
      //ForceFeedback
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `stVal.ps.new(3);
  loop{index i = 0; do:
    `stVal.ps[i].p = 0;
  while ++i < 3:
  }
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5030]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    `addYV(`stVal.hb.getYaccel());
    if(
      `mver0() == 1
      && !.m.inRange!int?(5000, 5199, `animno) && #`getAction(5030) > 0)
    {
      `trChangeAnim(5030);
    }
    if(`stVal.time == 0){
      `trHitVelSetX();
      `trHitVelSetY();
    }
    if(
      `trHitOver() || (
        `sysfvar[.fVY] > 0.0
        && `sysfvar[.fY] >= `sysfvar[.fAIR_GETHIT_GROUNDLEVEL]))
    {
      branch{
      cond !`isFall():
        `trChangeState(5040, -1, -1);
      else:
        `trChangeState(5050, -1, -1);
      }
      ret;
    }
    if(`trAnimTime() == 0){
      `trChangeState(5035, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `setCtrl(false);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5035]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(
      `stVal.time == 0 && #`getAction(5035) > 0
      && !.m.inRange!int?(5051, 5059, `animno) && `animno != 5090)
    {
      `trChangeAnim(5035);
    }
    `addYV(`stVal.hb.getYaccel());
    if(
      `trHitOver() || `trAnimTime() == 0
      || (
        `sysfvar[.fVY] > 0.0
        && `sysfvar[.fY] >= `sysfvar[.fAIR_GETHIT_GROUNDLEVEL])
      || (`stVal.time == 0 && `animno != 5035))
    {
      branch{
      cond !`isFall():
        `trChangeState(5040, -1, -1);
      else:
        `trChangeState(5050, -1, -1);
      }
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5040]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(!`isAlive()){
      `trChangeState(5050, -1, -1);
      ret;
    }
    if(
      (`trAnimTime() == 0 && `animno != 5040)
      || (`stVal.time == 0 && `animno != 5035))
    {
      `trChangeAnim(5040);
    }
    if(`trHitOver()){
      `setCtrl(true);
      `stVal.mov = .MovTy::I;
    }
    `addYV(`stVal.hb.getYaccel());
    if(`sysfvar[.fVY] > 0.0 && `sysfvar[.fY] >= 0.0){
      `trChangeState(52, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5050]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(
      (`trAnimTime() == 0 && `animno == 5035) || (
        `stVal.time == 0 && `animno != 5035
        && !.m.inRange!int?(5051, 5059, `animno)
        && !.m.inRange!int?(5061, 5069, `animno) && `animno != 5090))
    {
      `trChangeAnim(5050);
    }
    if(
      --`stVal.ps[0].p <= 0 && .m.inRange!int?(5050, 5059, `animno)
      && `sysfvar[.fVY] >= (`animno == 5050 ? 1.0 : -2.0)
      && #`getAction(`animno + 10) > 0)
    {
      `stVal.ps[0].p = .consts.int_t::MAX;
      `trChangeAnim(`animno + 10);
    }
    `addYV(`stVal.hb.getYaccel());
    if(`isAlive() && `trCanRecover() && `commandByName("recovery")){
      branch{
      cond
        `sysfvar[.fVY] > 0.0
        && `sysfvar[.fY]
        >= `sysfvar[.fAIR_GETHIT_GROUNDRECOVER_GROUND_THRESHOLD]
        :
        `trChangeState(5200, -1, -1);
        ret;
      cond `sysfvar[.fVY] > `sysfvar[.fAIR_GETHIT_AIRRECOVER_THRESHOLD]:
        `trChangeState(5210, -1, -1);
        ret;
      }
    }
    if(
      `sysfvar[.fVY] > 0.0 && `sysfvar[.fY] >= (
          .m.inRange!int?(5051, 5059, `animno)
          || .m.inRange!int?(5061, 5069, `animno) ? 0.0 : (
            `mver0() == 1 ? `sysfvar[.fAIR_GETHIT_GROUNDLEVEL] : 25.0)))
    {
      `trChangeState(5100, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `stVal.ps.new(1);
  `stVal.ps[0].p = 0;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5070]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    `trChangeAnim(5070);
    if(`trHitShakeOver()){
      `trChangeState(5071, -1, -1);
      ret;
    }
    if(`stVal.time == 0){
      //ForceFeedback
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5071]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      `trHitVelSetX();
      `trHitVelSetY();
    }
    `addYV(`stVal.hb.getYaccel());
    if(
      `sysfvar[.fVY] > 0.0
      && `sysfvar[.fY] >= `sysfvar[.fAIR_GETHIT_TRIP_GROUNDLEVEL])
    {
      `trChangeState(5110, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5080]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      `sysivar[2] = `stVal.hb.yvel == 0.0 ? 5080 : 5090;
      if(
        (
          .m.inRange!int?(5081, 5089, `animno)
          || .m.inRange!int?(5111, 5119, `animno))
        && #`getAction(`sysivar[2] + `animno%10) > 0)
      {
        `sysivar[2] += `animno%10;
      }
      if(`sysivar[2] == 5090 && #`getAction(5090) == 0) `sysivar[2] = 5030;
    }
    `trChangeAnim(`sysivar[2]);
    if(`trHitShakeOver()){
      branch{
      cond `stVal.hb.yvel == 0.0:
        `trChangeState(5081, -1, -1);
      else:
        `trChangeState(5030, -1, -1);
      }
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::L;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5081]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      `trHitVelSetX();
      `sysivar[0] = 1;
    }
    if(`trHitOver()){
      `setXV(0.0);
      `trChangeState(5110, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::L;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::C;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5100]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    switch(`stVal.time){
    case 0:
      if(
        `mver0() != 1 && `stVal.hb.fall.yvelocity == 0.0 && `sysfvar[.fVX] > 1.0)
      {
        `setXV(1.0);
      }
      .stage.envShake.time = `stVal.hb.fall.envshake_time;
      .stage.envShake.freq = (`stVal.hb.fall.envshake_freq / 180.0) * .m.PI;
      .stage.envShake.ampl = `stVal.hb.fall.envshake_ampl;
      .stage.envShake.phase = `stVal.hb.fall.envshake_phase;
      .stage.envShake.setDefPhase();
      `sysivar[1] = (int).m.floor(`sysfvar[.fVY]);
      branch{
      cond
        (
          !.m.inRange!int?(5051, 5059, `animno)
          && !.m.inRange!int?(5061, 5069, `animno))
        || #`getAction(5100 + `animno%10) == 0
        :
        `trChangeAnim(5100);
      else:
        `trChangeAnim(5100 + `animno%10);
      }
      `setY(0.0);
      `setYV(0.0);
      `mulXV(0.75);
      if(`stVal.hb.fall.yvelocity == 0.0){
        `trChangeState(5110, -1, -1);
        ret;
      }
    case 1:
      {
        index idx;
        ^&.Explod e = `newExplod(idx=);
        if(#e > 0){
          e~offsetx = 0.0;
          e~offsety = 0.0;
          branch{
          cond `mver0() == 1:
            e~id = 0;
            e~sprpriority = `sysivar[1] <= 14 ? -10 : 10;
          else:
            e~sprpriority = .consts.int_t::MIN;
            e~ownpal = 1;
            e~ontop = (int)(`sysivar[1] > 14);
            e~offsetx -= (float)`sysivar[.iDRAW_OFFSETX];
            e~offsety -= (float)`sysivar[.iDRAW_OFFSETY];
          }
          e~ani =
            .fight~getAction(
              60 + (int)(`sysivar[1] > 5) + (int)(`sysivar[1] > 14));
          e~setPos(`=);
          e~player=`id;
          `insertExplod(idx);
        }
        `playSound(
          true, false, false, 7,
          (int)(`sysivar[1] > 5) + (int)(`sysivar[1] > 14), -1,
          `sysivar[.iVOLUME], 0.0, 1.0, `sysfvar[.fX...fX+1]);
      }
    case 3:
      `trHitFallDamage();
    }
    `sysivar[.iSPECIALFLAG] |= .lsPOSFREEZEX | .lsPOSFREEZEY;
    if(`trAnimTime() == 0){
      `trChangeState(5101, -1, -1);
      ret;
    }
    if(`stVal.time == 0){
      //ForceFeedback
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::L;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5101]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    switch(`stVal.time){
    case 0:
      branch{
      cond
        !.m.inRange!int?(5101, 5109, `animno)
        || #`getAction(5160 + `animno%10) == 0
        :
        `trChangeAnim(5160);
      else:
        `trChangeAnim(5160 + `animno%10);
      }
      `trHitFallVel();
      `setY(`sysfvar[.fDOWN_BOUNCE_OFFSETY]);
      `addX(`sysfvar[.fDOWN_BOUNCE_OFFSETX]);
    }
    `addYV(`sysfvar[.fDOWN_BOUNCE_YACCEL]);
    if(
      `sysfvar[.fVY] > 0.0
      && `sysfvar[.fY] >= `sysfvar[.fDOWN_BOUNCE_GROUNDLEVEL])
    {
      `trChangeState(5110, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::L;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5110]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(
      --`stVal.ps[0].p <= 0 && #`getAction(5110 + `animno%10) > 0
      && .m.inRange!int?(5081, 5089, `animno))
    {
      `stVal.ps[0].p = .consts.int_t::MAX;
      `trChangeAnim(5110 + `animno%10);
    }
    if(`stVal.time == 0){
      .stage.envShake.time = `stVal.hb.fall.envshake_time;
      .stage.envShake.freq = (`stVal.hb.fall.envshake_freq / 180.0) * .m.PI;
      .stage.envShake.ampl = `stVal.hb.fall.envshake_ampl;
      .stage.envShake.phase = `stVal.hb.fall.envshake_phase;
      .stage.envShake.setDefPhase();
      if(!.m.inRange!int?(5110, 5119, `animno)) branch{
      cond
        !.m.inRange!int?(5161, 5169, `animno)
        || #`getAction(5170 + `animno%10) == 0
        :
        `trChangeAnim(5170);
      else:
        `trChangeAnim(5170 + `animno%10);
      }
      `trHitFallDamage();
      `setY(0.0);
      if(`stVal.hb.fall.yvelocity != 0.0){
        `sysivar[1] = (int).m.floor(`sysfvar[.fVY]);
      }
      if(`sysivar[0] == 0){
        `playSound(
          true, false, false, 7,
          (int)(`sysivar[1] > 5) + (int)(`sysivar[1] > 14), -1,
          `sysivar[.iVOLUME], 0.0, 1.0, `sysfvar[.fX...fX+1]);
        index idx;
        ^&.Explod e = `newExplod(idx=);
        if(#e > 0){
          e~offsetx = 0.0;
          e~offsety = 0.0;
          branch{
          cond `mver0() == 1:
            e~id = 0;
            e~sprpriority = `sysivar[1] <= 14 ? -10 : 10;
          else:
            e~sprpriority = .consts.int_t::MIN;
            e~ownpal = 1;
            e~ontop = (int)(`sysivar[1] > 14);
            e~offsetx -= (float)`sysivar[.iDRAW_OFFSETX];
            e~offsety -= (float)`sysivar[.iDRAW_OFFSETY];
          }
          e~ani =
            .fight~getAction(
              60 + (int)(`sysivar[1] > 5) + (int)(`sysivar[1] > 14));
          e~setPos(`=);
          e~player=`id;
          `insertExplod(idx);
        }
      }
      `setYV(0.0);
    }
    if(
      --`stVal.ps[1].p <= 0 && .m.inRange!int?(5171, 5179, `animno)
      && #`getAction(5110 + `animno%10) > 0 && (
        `trAnimTime() == 0 || `sysivar[0] != 0))
    {
      `stVal.ps[1].p = .consts.int_t::MAX;
      `trChangeAnim(5110 + `animno%10);
    }
    if(
      --`stVal.ps[2].p <= 0 && !.m.inRange!int?(5111, 5119, `animno) && (
        `trAnimTime() == 0 || `sysivar[0] != 0))
    {
      `stVal.ps[2].p = .consts.int_t::MAX;
      `trChangeAnim(5110);
    }
    if(
      !`isAlive() && (
        `trAnimTime() == 0 || `sysivar[0] != 0
        || .m.inRange!int?(5110, 5119, `animno)))
    {
      `trChangeState(5150, -1, -1);
      ret;
    }
    if(`stVal.time == 0){
      `sysivar[0] = 0;
      branch{
      cond `isAlive():
        //ForceFeedback
      else:
        //ForceFeedback
      }
    }
    `mulXV(0.85);
    if(`mver0() == 1 && #`sysfvar[.fVX] < `sysfvar[.fDOWN_FRICTION_THRESHOLD]){
      `setXV(0.0);
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::L;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `stVal.ps.new(3);
  loop{index i = 0; do:
    `stVal.ps[i].p = 0;
  while ++i < 3:
  }
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5120]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      branch{
      cond
        !.m.inRange!int?(5111, 5119, `animno)
        || #`getAction(5120 + `animno%10) == 0
        :
        `trChangeAnim(5120);
      else:
        `trChangeAnim(5120 + `animno%10);
      }
      `setXV(0.0);
    }
    `stVal.hb.hitf1 = -8;
    `stVal.hb.hitt1 = 1;
    if(`trAnimTime() == 0){
      `trHitFallSet(1, 0.0/0.0, 0.0/0.0);
      `stVal.hb.hitf1 = -9345;
      `stVal.hb.hitt1 = 12;
      `stVal.hb.hitf2 = -8;
      `stVal.hb.hitt2 = 3;
      `trChangeState(0, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::L;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5150]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0){
      branch{
      cond
        (
          !.m.inRange!int?(5111, 5119, `animno)
          && !.m.inRange!int?(5171, 5179, `animno))
        || #`getAction(5140 + `animno%10) == 0
        :
        if(#`getAction(5140) > 0) `trChangeAnim(5140);
      else:
        `trChangeAnim(5140 + `animno%10);
      }
      if(
        !.m.inRange!int?(5140, 5149, `animno)
        && !.m.inRange!int?(5110, 5119, `animno))
      {
        `trChangeAnim(5110);
      }
    }
    if(
      --`stVal.ps[0].p <= 0 && .com.matchOver()
      && .m.inRange!int?(5140, 5149, `animno) && #`getAction(`animno + 10) > 0)
    {
      `stVal.ps[0].p = .consts.int_t::MAX;
      `trChangeAnim(`animno + 10);
    }
    `mulXV(0.85);
    if(
      --`stVal.ps[1].p <= 0
      && `sysfvar[.fVX] < `sysfvar[.fDOWN_FRICTION_THRESHOLD])
    {
      `stVal.ps[1].p = .consts.int_t::MAX;
      `setXV(0.0);
    }
    `stVal.hb.hitf1 = -8;
    `stVal.hb.hitt1 = 1;
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::L;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `stVal.ps.new(2);
  loop{index i = 0; do:
    `stVal.ps[i].p = 0;
  while ++i < 2:
  }
  `sprpriority = -3;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5200]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`animno == 5035 && `trAnimTime() == 0) `trChangeAnim(5050);
    `addYV(`stVal.hb.getYaccel());
    if(
      `sysfvar[.fVY] > 0.0
      && `sysfvar[.fY] >= `sysfvar[.fAIR_GETHIT_GROUNDRECOVER_GROUNDLEVEL])
    {
      `trSelfState(5201, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5201]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    switch(`stVal.time){
    case 0:
      if(`trP2DistX()$.v.toF() < -5.0) `setFacing(-`facing);
      `setXV(`sysfvar[.fAIR_GETHIT_GROUNDRECOVERX]);
      `setYV(`sysfvar[.fAIR_GETHIT_GROUNDRECOVERY]);
      `setY(0.0);
      {
        ^&.com.PalFX fx = `getPalfx();
        fx~clear();
        fx~time = 3;
        fx~addr = 128;
        fx~addg = 128;
        fx~addb = 128;
      }
    case 1:
      {
        index idx;
        ^&.Explod e = `newExplod(idx=);
        if(#e > 0){
          e~offsetx = 0.0;
          e~offsety = 0.0;
          branch{
          cond `mver0() == 1:
            e~id = 0;
            e~sprpriority = -10;
          else:
            e~sprpriority = .consts.int_t::MIN;
            e~ownpal = 1;
            e~offsetx -= (float)`sysivar[.iDRAW_OFFSETX];
            e~offsety -= (float)`sysivar[.iDRAW_OFFSETY];
          }
          e~ani = .fight~getAction(60);
          e~setPos(`=);
          e~player=`id;
          `insertExplod(idx);
        }
      }
    }
    `stVal.hb.hitf1 = -8;
    `stVal.hb.hitt1 = 1;
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::H;
  `stVal.phy = .StTy::A;
  `trChangeAnim(5200);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5210]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    switch(`stVal.time){
    case 0:
      {
        ^&.com.PalFX fx = `getPalfx();
        fx~clear();
        fx~time = 3;
        fx~addr = 128;
        fx~addg = 128;
        fx~addb = 128;
      }
      `sysivar[.iSPECIALFLAG] |= .lsPOSFREEZEX | .lsPOSFREEZEY;
      if(`trP2DistX()$.v.toF() < -20.0) `setFacing(-`facing);
      `stVal.hb.hitf1 = -8;
      `stVal.hb.hitt1 = 15;
    case 4:
      branch{
      cond `mver0() == 1:
        `mulXV(`sysfvar[.fAIR_GETHIT_AIRRECOVER_MULX]);
        `mulYV(`sysfvar[.fAIR_GETHIT_AIRRECOVER_MULY]);
      else:
        `mulXV(0.8);
        `mulYV(0.8);
      }
      `addXV(`sysfvar[.fAIR_GETHIT_AIRRECOVER_ADDX]);
      `addYV(`sysfvar[.fAIR_GETHIT_AIRRECOVER_ADDY]);
      if(`mver0() != 1){
        if(`sysfvar[.fVY] > 0.0) `mulYV(0.5);
        if(`sysfvar[.fVY] > -3.0) `addYV(-2.0);
        if(`sysfvar[.fVY] > -2.0) `addYV(-1.0);
      }
      if(`commandByName("holdup")){
        `addYV(`sysfvar[.fAIR_GETHIT_AIRRECOVER_UP]);
      }
      if(`commandByName("holddown")){
        `addYV(`sysfvar[.fAIR_GETHIT_AIRRECOVER_DOWN]);
      }
      if(`commandByName("holdfwd")){
        `addXV(`sysfvar[.fAIR_GETHIT_AIRRECOVER_FWD]);
      }
      if(`commandByName("holdback")){
        `addXV(`sysfvar[.fAIR_GETHIT_AIRRECOVER_BACK]);
      }
    case 20:
      `setCtrl(true);
    }
    if(`mver0() == 1 && `stVal.time < 4){
      `sysivar[.iSPECIALFLAG] |= .lsPOSFREEZEX | .lsPOSFREEZEY;
    }
    if(`stVal.time >= 4) `addYV(`sysfvar[.fAIR_GETHIT_AIRRECOVER_YACCEL]);
    if(`sysfvar[.fVY] > 0.0 && `sysfvar[.fY] >= 0.0){
      `trChangeState(52, -1, 1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::A;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `trChangeAnim(5210);
  `setCtrl(false);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5500]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(`stVal.time == 0 && #`getAction(5500) > 0) `trChangeAnim(5500);
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `setCtrl(false);
  `trChangeAnim(5300);
  `setXV(0.0);
  `setYV(0.0);
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}


[Statedef 5900]
SSZ_CODE:
void c.Char::s##_@@P()
{
  if(!`isHitPaused()){
    if(.com.rexisted[`playerno&1] == 0){
      `rangeSetIVar(0, 59, 0);
      `rangeSetFVar(0, 39, 0.0);
    }
    if(.cgi[`playerno].sf~head.ver0 != 0d1){
      `trRemapPal(
        1, 1, 1, `helperindex == 0 ? .cgi[`playerno].drawpalno : 1);
    }
    branch{
    cond .com.round == 1:
      `trChangeState(190, -1, -1);
      ret;
    cond `mver0() == 1:
      `trChangeState(0, -1, -1);
      ret;
    }
  }
}
void c.Char::s##_@@PInit()
{
  `stVal.typ = .StTy::S;
  `stVal.mov = .MovTy::I;
  `stVal.phy = .StTy::N;
  `clearHitCount();
  `clearHitDef();
  `clearMoveHit();
  `stVal.juggle = 0;
}

